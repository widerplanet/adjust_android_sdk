apply plugin: 'com.android.library'
apply plugin: 'maven-publish'
apply plugin: 'signing'

repositories {
    maven {
        url "https://oss.sonatype.org/content/repositories/staging/"
    }
}

android {
    compileSdkVersion rootProject.ext.coreCompileSdkVersion

    defaultConfig {
        minSdkVersion rootProject.ext.coreMinSdkVersion
        targetSdkVersion rootProject.ext.coreTargetSdkVersion
        versionName rootProject.ext.coreVersionName
        versionCode rootProject.ext.defaultVersionCode
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // Add SDK via module.
    implementation project(':sdk-core')
    // Add SDK via Maven.
    // implementation 'com.adjust.sdk:adjust-android:4.16.0'
}

task adjustWiderPlanetAndroidJar(type: Jar) {
    dependsOn 'compileReleaseJavaWithJavac'
    from('build/intermediates/javac/release/compileReleaseJavaWithJavac/classes/')
    archiveName "${project.name}.jar"
}

task androidJavadocs(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    // JDK 1.8 is more strict then 1.7. Have JDK 1.8 behave like 1.7 for javadoc generation.
    if (org.gradle.internal.jvm.Jvm.current().getJavaVersion() == JavaVersion.VERSION_1_8) {
        options.addStringOption('Xdoclint:none', '-quiet')
    }
}

task adjustWiderPlanetAndroidJavadocsJar(type: Jar) {
    dependsOn 'androidJavadocs'
    classifier = 'javadoc'
    from androidJavadocs.destinationDir
}

task adjustWiderPlanetAndroidSourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

artifacts {
    archives adjustWiderPlanetAndroidJar
    archives adjustWiderPlanetAndroidJavadocsJar
    archives adjustWiderPlanetAndroidSourcesJar
}

publishing {
    publications {
        mavenAndroidWiderPlanet(MavenPublication) {
            customizePom(pom)
            groupId rootProject.ext.adjustGroupId
            artifactId 'adjust-android-widerplanet'
            version rootProject.ext.coreVersionName

            // Create the signed POM artifact.
            pom.withXml {
                def pomFile = file("${project.buildDir}/generated-pom.xml")
                writeTo(pomFile)
                def pomAscFile = signing.sign(pomFile).signatureFiles[0]
                artifact(pomAscFile) {
                    classifier = null
                    extension = 'pom.asc'
                }
            }

            // Create the signed artifacts.
            project.tasks.signArchives.signatureFiles.each {
                // exclude "usual" archive artifact .aar
                def signFileName = it.toString()
                if (signFileName.contains('aar')) {
                    return
                }
                // Create a Maven artifact for each asc signature
                artifact(it) {
                    if (signFileName.contains('-sources')) {
                        classifier = 'sources'
                        extension = 'jar.asc'
                    } else if (signFileName.contains('-javadoc')) {
                        classifier = 'javadoc'
                        extension = 'jar.asc'
                    } else {
                        classifier = null
                        extension = 'jar.asc'
                    }
                }
            }

            artifact adjustWiderPlanetAndroidJar
            artifact adjustWiderPlanetAndroidJavadocsJar
            artifact adjustWiderPlanetAndroidSourcesJar
        }
    }

    repositories {
        maven {
            url "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            if (project.hasProperty("sonatypeUsername")) {
                credentials {
                    username sonatypeUsername
                    password sonatypePassword
                }
            }
        }
    }
}

def customizePom(pom) {
    pom.withXml {
        def root = asNode()

        // Add all items necessary for maven central publication
        root.children().last() + {
            resolveStrategy = Closure.DELEGATE_FIRST
            description 'The WiderPlanet plugin for Adjust SDK for Android'
            name 'Adjust Android SDK WiderPlanet plugin'
            url 'https://github.com/adjust/android_sdk'

            organization {
                name 'adjust GmbH'
                url 'http://www.adjust.com'
            }
            licenses {
                license {
                    name 'MIT License'
                    url 'http://www.opensource.org/licenses/mit-license.php'
                }
            }
            scm {
                url 'git@github.com:adjust/android_sdk.git'
                connection 'scm:git:git@github.com:adjust/android_sdk.git'
                developerConnection 'scm:git:git@github.com:adjust/android_sdk.git'
            }
            developers {
                developer {
                    name 'Pedro Silva'
                    email 'pedro@adjust.com'
                }
                developer {
                    name 'Ugljesa Erceg'
                    email 'ugljesa@adjust.com'
                }
            }
        }
    }
}

model {
    tasks.generatePomFileForMavenAndroidWiderPlanetPublication {
        destination = file("${project.buildDir}/generated-pom.xml")
    }
    tasks.publishMavenAndroidWiderPlanetPublicationToMavenLocal {
        dependsOn project.tasks.signArchives
    }
    tasks.publishMavenAndroidWiderPlanetPublicationToMavenRepository {
        dependsOn project.tasks.signArchives
    }
}

signing {
    sign configurations.archives
}
